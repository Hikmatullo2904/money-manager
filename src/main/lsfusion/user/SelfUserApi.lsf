MODULE SelfUserApi;

REQUIRE SelfUser, UtilResponse, Time, UtilString, Account;


password = DATA LOCAL STRING ();
email = DATA LOCAL STRING[400] ();
firstName = DATA LOCAL STRING[100] ();
lastName = DATA LOCAL STRING[100] ();

isEmail(STRING email) = contains('@', email);

customUser() = DATA LOCAL CustomUser();

FORM regiterRequestForm
    PROPERTIES() password, email, firstName, lastName
;

login(FILE f) {
    IMPORT JSON FROM f TO() password, email;
    customUser() <- findByEmail(email());
    IF NOT customUser() THEN {
        returnError(102);
        RETURN ;
    }
    calculateBase64Hash('SHA-256', password());
    IF NOT calculatedHash() == sha256Password(customUser()) THEN {
        returnError(102);
        RETURN ;
    }
    generateAuthToken(login(customUser()));
    EXPORT JSON FROM token = resultAuthToken();
}@@noauth;

register(FILE f) {
    IMPORT regiterRequestForm JSON FROM f;
    
    IF NOT (password() AND email() AND firstName() AND lastName()) THEN {
        returnError(405);
        RETURN;
    }
    
    IF findByEmail(email()) THEN {
        returnError(101);
        RETURN ;
    }
    IF NOT isEmail(email()) THEN {
        returnError('Please provide valid email');
        RETURN ;
    }
    
    
    
    NEW su = CustomUser {
        email(su) <- email();
        firstName(su) <- firstName();
        lastName(su) <- lastName();
        setSHA256Password(su, password());
        createdAt(su) <- currentDateTime();
        status(su) <- SelfUserStatus.verified; 
        login(su) <- email();
        customUser() <- su;
    }
    APPLY ;
    
    returnSuccessResponse();
    
}@@noauth;

FORM meResponseForm
    OBJECTS data = CustomUser
    PROPERTIES(data) firstName, lastName, email
;

me() {
    customUser() <- currentUser();
    IF NOT customUser() THEN {
        returnError(401);
        RETURN ;
    }
    EXPORT meResponseForm OBJECTS data = currentUser() JSON;
    
}@@api;
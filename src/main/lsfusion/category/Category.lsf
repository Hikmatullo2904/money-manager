MODULE Category;

REQUIRE SelfUser, TransactionType;

CLASS Category;

name 'Название категории' = DATA STRING (Category);
createdAt = DATA DATETIME (Category);
user = DATA CustomUser (Category);
isDeleted = DATA BOOLEAN (Category);
type = DATA TransactionType(Category);


findByName(STRING name, CustomUser s) = GROUP LAST Category c IF name(c) == name AND user(c) == s ORDER c;
categoryById(LONG id, CustomUser s) = GROUP LAST Category c IF LONG(c AS Category) == id AND user(c) == s ORDER c;
id(Category c) = LONG (c AS Category);
category = DATA LOCAL Category ();

categoryType 'Тип категории' = DATA LOCAL TransactionType();

CONSTRAINT Category c IS Category AND NOT type(c) MESSAGE 'Пожалуйста, выберите тип категории';


deleteCategoryForm(Category c) {
    IF NOT user(c) THEN {
        MESSAGE 'Категории приложений не могут быть удалены';
        RETURN;
    }
    isDeleted(c) <- TRUE;
    APPLY ;
}

FORM UserCategories 'Категории' 
    OBJECTS c = Category
    PROPERTIES READONLY name = name(c), type 'тип' = typeName(type(c))
    PROPERTIES (c) NEWSESSION NEW, EDIT, deleteCategoryForm
    PROPERTIES categoryType 'Тип категории' = typeName(categoryType())
    FILTERS NOT isDeleted(c) 
    AND user(c) == currentUser()
    AND (NOT categoryType() OR categoryType() == type(c))
;



FORM category 'Категория' 
    OBJECTS c = Category PANEL
    PROPERTIES(c) name
    PROPERTIES type 'тип' = typeName(type(c))
    EDIT Category OBJECT c
;

WHEN SET(Category c IS Category) DO {
    createdAt(c) <- currentDateTime();
    IF NOT user(c) THEN {
        user(c) <- currentUser();
    }
 }
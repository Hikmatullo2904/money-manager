MODULE AccountApi;

REQUIRE Account, UtilResponse, SelfUser, Currency;

name = DATA LOCAL STRING();
balance = DATA LOCAL LONG();
currencyId = DATA LOCAL LONG ();
id = DATA LOCAL LONG();

addAccount(FILE f) {
    IMPORT JSON FROM f TO() name, balance, currencyId;
    
    IF NOT name() THEN {
        returnError(405);
        RETURN;
    }
    
    IF findByName(name(), currentUser() AS CustomUser) THEN {
        returnError(302);
        RETURN;
    }
    
    IF balance() <= 0 THEN {
        returnError(307);
        RETURN ;
    }
    
    IF NOT currencyId() THEN {
        currency() <- currency(currentUser() AS CustomUser);
    }ELSE {
        currency() <- currencyById(currencyId());
    }
    IF NOT currency() THEN {
        returnError(310);
        RETURN;
    }
    
    NEW a = Account {
        name(a) <- name();
        balance(a) <- balance();
        currency(a) <- currency();
        user(a) <- currentUser();
    }
    
    APPLY ;
    returnSuccessResponse();
}@@api;
 
updateAccount(FILE f) {
    IMPORT JSON FROM f TO() id, name, balance, currencyId;

    account() <- accountById(id(), currentUser() AS CustomUser);
    IF NOT account() THEN {
        returnError(304);
        RETURN;
    }


    IF NOT name() THEN {
        returnError(405);
        RETURN;
    }
    account() <- findByName(name(), currentUser() AS CustomUser);
    IF account() AND account() != accountById(id()) THEN {
        returnError(302);
        RETURN;
    }

    IF balance() <= 0 THEN {
        returnError(307);
        RETURN ;
    }
    
    IF NOT currencyId() THEN {
        currency() <- currency(currentUser() AS CustomUser);
    }ELSE {
        currency() <- currencyById(currencyId());
    }
    
    account() <- account();
    name(account()) <- name();
    balance(account()) <- balance();
    currency(account()) <- currency();
    APPLY ;
    returnSuccessResponse();
}@@api;

deleteAccount(LONG id) {
    account() <- accountById(id, currentUser() AS CustomUser);
    IF NOT account() THEN {
        returnError(304);
        RETURN;
    }
    IF user(account()) != currentUser() THEN {
        returnError(303);
        RETURN;
    }
    isDeleted(account()) <- TRUE;
    APPLY ;
    returnSuccessResponse();
}@@api;

FORM getAllAccountsResponse
    OBJECTS data = Account
    PROPERTIES(data) id, name, balance
    PROPERTIES currency = getCurrencyName(currency(data))
    FILTERS user(data) == currentUser() AND NOT isDeleted(data)
;

getAccounts() {
    EXPORT getAllAccountsResponse JSON;
}@@api;

accountFrom = DATA LOCAL LONG();
accountTo = DATA LOCAL LONG();
amount = DATA LOCAL LONG();

transferMoney(FILE f) {
    IMPORT JSON FROM f TO() accountFrom, accountTo, amount;
    IF NOT (accountFrom() AND accountTo() AND amount()) THEN {
        returnError(405);
        RETURN;
    }
    LOCAL sourceAccount = Account();
    LOCAL targetAccount = Account();
    sourceAccount() <- accountById(accountFrom(), currentUser() AS CustomUser);
    IF NOT sourceAccount() THEN {
        returnError(304);
        RETURN;
    }

    targetAccount() <- accountById(accountTo(), currentUser() AS CustomUser);
    IF NOT targetAccount() THEN {
        returnError(304);
        RETURN;
    }
    
    IF balance(sourceAccount()) < amount() THEN {
        returnError(307);
        RETURN ;
    }
    
    NEW t = Transers {
        sourceAccount(t) <- sourceAccount();
        targetAccount(t) <- targetAccount();
        amount(t) <- amount();
    }
    
    APPLY ;
    returnSuccessResponse();
}@@api;